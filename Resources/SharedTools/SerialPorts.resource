*** Settings ***
Documentation  This resource is mean to be used for reading and validating the serial logs that are 
...  generated by the Device and read by the computer. To use it, it is needed to set the com ports 
...  numbers where they are physically connected, in the titanConfig->ParametersConf.json file.
...  TitanLibrary must be installed in the python environment.

Library    TitanLibrary.handlers.serialdriver.SerialDriver

*** Keywords ***
Log Serial Ports To File
    [Documentation]  If this keyword is called, be sure the use Close Port at the end of the TC.
    Log To File Or Console  file

Log Serial Ports To File N Seconds
    [Arguments]  ${seconds}
    Log To File Or Console  file
    Sleep  ${seconds}
    [Teardown]  close port

String Appears in X min Y Times in Log
    [Arguments]  ${specified_string}  ${time_to_wait}  ${occurences}  ${log_name}
    ${result}  String Found in Log n Times  ${specified_string}  ${time_to_wait}  ${occurences}  ${log_name}
    Should Be True  ${result}
    [Teardown]  Close Port

String Dont Appears in X min Y Times in Log
    [Arguments]  ${specified_string}  ${time_to_wait}  ${occurences}  ${log_name}
    ${result}  String Found in Log n Times  ${specified_string}  ${time_to_wait}  ${occurences}  ${log_name}
    Should Not Be True  ${result}
    [Teardown]  Close Port

Get n Lines Containing a String from Log
    [Arguments]  ${number_of_lines}  ${specified_string}  ${log_name}    
    ${app_path}  Get Log File Path  ${log_name}
    @{list_with_strings}  Return n Lines With String  ${specified_string}  ${number_of_lines}  ${app_path}
    [Return]  ${list_with_strings}

String is in The Given List
    [Arguments]  ${contained_string}  ${list_with_strings}
    ${result}  Set Variable  ${FALSE}
    FOR  ${element}  IN  @{list_with_strings}
        ${result}  Run Keyword and Return Status  should contain  ${element}  ${contained_string}
        Exit For Loop If  ${result}==${TRUE}
    END
    Should Be True  ${result}

N Number of Filtered Lines of Log File Should Contain
    [Arguments]  ${number_of_lines}  ${specified_string}  ${log_name}  ${contained_string}  
    ${list_with_strings}  Get N Lines Containing A String From Log  ${number_of_lines}  ${specified_string}  ${log_name}   
    String Is In The Given List  ${contained_string}  ${list_with_strings}

String Found in Serial Log File
    [Arguments]  ${string_to_find}  ${log_name}
    ${path}  Get Log File Path  ${log_name}
    ${result}  File Contains String  ${string_to_find}  ${path}
    Should Be True  ${result}

String not Found in Serial Log File
    [Arguments]  ${string_to_find}  ${log_name}
    ${path}  Get Log File Path  ${log_name}
    ${result}  File Contains String  ${string_to_find}  ${path}
    Should Not Be True  ${result}

Word is in Log File More Than N Times
    [Arguments]  ${sting_to_find}  ${log_name}  ${occurrences}
    ${path}  Get Log File Path  ${log_name}
    ${result}  Count Word in File  ${sting_to_find}  ${path}
    should be true  ${result} > ${occurrences}

Serial Log File Contains Words
    [Arguments]  ${log_name}  @{words}
    ${path}  Get Log File Path  ${log_name}
    ${result}  Verify Multiple Strings in File  ${words}  ${path}
    should be true  ${result}

Close Serial Ports
    close port