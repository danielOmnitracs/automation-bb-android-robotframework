*** Settings ***
Documentation  This resource is mean to be used for reading and validating the serial logs that are 
...  generated by the Device and read by the computer. To use it, it is needed to set the com ports 
...  numbers where they are physically connected, in titanConfig->ParametersConf.json file, at the 
...  of the project. TitanLibrary must be installed in the python environment.
...  Only "String Appears in X min Y Times in Log" and "String Dont Appears in X min Y Times in Log"
...  are done in real time. The other methods are meant to be used after we have saved a file.

Library    TitanLibrary.handlers.serialdriver.SerialDriver

*** Keywords ***
Log Serial Ports To File
    [Documentation]  If this keyword is called, be sure the use Close Port at the end of the TC.
    Log To File Or Console  file

Log Serial Ports To File N Seconds
    [Documentation]  It will save the log to a file, named as the test case name with a time stamp,
    ...  the log will last as much as it is specified in ${seconds}
    [Arguments]  ${seconds}
    Log To File Or Console  file
    Sleep  ${seconds}
    [Teardown]  Close Port

String Appears in X min Y Times in Log
    [Documentation]  The string ${string_to_find} appears within X min ${time_to_wait}, 
    ...  Y times ${occurences} in the specified serial log ${log_name}. This is done while 
    ...  the log is being monitored.
    [Arguments]  ${string_to_find}  ${time_to_wait}  ${occurences}  ${log_name}
    ${result}  String Found in Log n Times  ${string_to_find}  ${time_to_wait}  ${occurences}  ${log_name}
    Should Be True  ${result}

String Dont Appears in X min Y Times in Log
    [Documentation]  The string ${string_to_find} doesnt appears within X min ${time_to_wait}, 
    ...  Y times ${occurences} in the specified serial log ${log_name}. This is done while 
    ...  the log is being monitored.
    [Arguments]  ${string_to_find}  ${time_to_wait}  ${occurences}  ${log_name}
    ${result}  String Found in Log n Times  ${string_to_find}  ${time_to_wait}  ${occurences}  ${log_name}
    Should Not Be True  ${result}

Get N Lines Containing a String from Log
    [Documentation]  Once a log was saved, return n lines ${number_of_lines} that contains
    ...  ${string_to_find} in the specified serial log ${log_name}
    [Arguments]  ${number_of_lines}  ${string_to_find}  ${log_name}    
    ${app_path}  Get Log File Path  ${log_name}
    @{list_with_strings}  Return n Lines With String  ${string_to_find}  ${number_of_lines}  ${app_path}
    [Return]  ${list_with_strings}

String Is In The Given List
    [Documentation]  Checks if a string in inside at least one of the elements of ${list_with_string}
    [Arguments]  ${string}  ${list_with_strings}
    ${result}  Set Variable  ${FALSE}
    FOR  ${element}  IN  @{list_with_strings}
        ${result}  Run Keyword and Return Status  Should Contain  ${element}  ${string}    
        Exit For Loop If  ${result}==${TRUE}
    END
    Should Be True  ${result}

N Number of Filtered Lines of Log File Should Contain a String
    [Documentation]  Logs n lines ${number_of_lines} of the specified serial log ${log_name}
    ...  which contains the string ${string_filter} and then within those lines check if 
    ...  the string ${specified_string} is present
    [Arguments]  ${number_of_lines}  ${string_filter}  ${log_name}  ${specified_string}  
    ${list_with_strings}  Get N Lines Containing A String From Log  ${number_of_lines}  ${string_filter}  ${log_name}   
    String Is In The Given List  ${specified_string}  ${list_with_strings}

String Found in Serial Log File
    [Documentation]  Check if an already saved log file from the serial port ${log_name} has a string ${string_to_find}
    [Arguments]  ${string_to_find}  ${log_name}
    ${path}  Get Log File Path  ${log_name}
    ${result}  File Contains String  ${string_to_find}  ${path}
    Should Be True  ${result}

String not Found in Serial Log File
    [Documentation]  Check if an already saved log file from the serial port ${log_name} doesnt have a string ${string_to_find}
    [Arguments]  ${string_to_find}  ${log_name}
    ${path}  Get Log File Path  ${log_name}
    ${result}  File Contains String  ${string_to_find}  ${path}
    Should Not Be True  ${result}

Word is in Log File More Than N Times
    [Documentation]  A word ${sting_to_find} appears more than n times ${occurrences} in an already saved file of
    ...  serial port  ${log_name}
    [Arguments]  ${sting_to_find}  ${log_name}  ${occurrences}
    ${path}  Get Log File Path  ${log_name}
    ${result}  Count Word in File  ${sting_to_find}  ${path}
    should be true  ${result} > ${occurrences}

Serial Log File Contains Words
    [Documentation]  The list of words @{words} exist in an already saved log file of the serial port ${log_name}
    [Arguments]  ${log_name}  @{words}
    ${path}  Get Log File Path  ${log_name}
    ${result}  Verify Multiple Strings in File  ${words}  ${path}
    should be true  ${result}

Close Serial Ports
    close port